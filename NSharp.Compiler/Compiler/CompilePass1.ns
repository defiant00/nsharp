use System.Reflection
use System.Reflection.Metadata
use System.Reflection.Metadata.Ecma335
use NSharp.Compiler.Tree
use NSharp.Core

ns NSharp.Compiler

; Pass 1, creates:
;   * The module and assembly.
;   * Class headers.
public class CompilePass1
    public fn Run(state State)
        var guid = Guid.NewGuid()

        state.PeMetadataBuilder.AddModule(
            0,
            state.PeMetadataBuilder.GetOrAddString("{state.FileName}.dll"),
            state.PeMetadataBuilder.GetOrAddGuid(guid),
            default,
            default
        )

        var assemblyHandle = state.PeMetadataBuilder.AddAssembly(
            state.PeMetadataBuilder.GetOrAddString(state.FileName),
            new Version(1, 0, 0, 0),
            default,
            default,
            0,
            AssemblyHashAlgorithm.None
        )

        if path = Path.GetDirectoryName(typeof(System.Object).Module.FullyQualifiedName), path != null
            state.TypeResolver.AddAssemblyPath(path)
        state.TypeResolver.AddAssemblyRef("System.Runtime")
        state.TypeResolver.AddAssemblyRef("System.Console")

        state.ObjectTypeRef = state.TypeResolver.GetOrAddTypeRef("System", "Object")

        ; TargetFrameworkAttribute
        var targetFrameworkTypeRef = state.TypeResolver.GetOrAddTypeRef("System.Runtime.Versioning", "TargetFrameworkAttribute")

        var targetFrameworkCtorSig = new BlobBuilder()
        new BlobEncoder(targetFrameworkCtorSig)
            .MethodSignature(isInstanceMethod = true)
            .Parameters(1, fn(r) is r.Void(), fn(p) is p.AddParameter().Type().String())

        var targetFrameworkMemberRef = state.PeMetadataBuilder.AddMemberReference(
            targetFrameworkTypeRef,
            state.PeMetadataBuilder.GetOrAddString(".ctor"),
            state.PeMetadataBuilder.GetOrAddBlob(targetFrameworkCtorSig)
        )

        var targetFrameworkAttribute = new BlobBuilder()
        new BlobEncoder(targetFrameworkAttribute)
            .CustomAttributeSignature(
                fn(fa) is fa.AddArgument().Scalar().Constant(".NETCoreApp,Version=v6.0"),
                fn(na) is na.Count(1).AddArgument(
                    false,
                    fn(t) is t.ScalarType().String(),
                    fn(n) is n.Name("FrameworkDisplayName"),
                    fn(l) is l.Scalar().Constant("")
                )
            )
        
        state.PeMetadataBuilder.AddCustomAttribute(
            assemblyHandle,
            targetFrameworkMemberRef,
            state.PeMetadataBuilder.GetOrAddBlob(targetFrameworkAttribute)
        )

        ; <Module>
        state.PeMetadataBuilder.AddTypeDefinition(
            default,
            default,
            state.PeMetadataBuilder.GetOrAddString("<Module>"),
            default,
            MetadataTokens.FieldDefinitionHandle(1),
            MetadataTokens.MethodDefinitionHandle(1)
        )

        for c in state.Classes
            ProcessClass(state, c)
    
    private fn ProcessClass(state State, cl Class)
        if cl.Handle != null
            ret
        
        cl.Emitting = true

        var parent = state.ObjectTypeRef
        if cl.TreeItem.Parent != null
            ; parse the expression and look up the type
            ; if it's part of the current assembly, emit it
        
        var attrs = TypeAttributes.Class | TypeAttributes.AutoLayout | TypeAttributes.BeforeFieldInit
        if cl.TreeItem.Modifiers.Any(fn(m) is m.Type == ModifierType.Public)
            attrs |= TypeAttributes.Public

        state.PeMetadataBuilder.AddTypeDefinition(
            attrs,
            state.PeMetadataBuilder.GetOrAddString(cl.Namespace),
            state.PeMetadataBuilder.GetOrAddString(cl.TreeItem.Name),
            parent,
            MetadataTokens.FieldDefinitionHandle(1),
            MetadataTokens.MethodDefinitionHandle(1)
        )

        cl.Emitting = false
        state.ClassOrder.Add(cl)
        
        for c in cl.Classes
            ProcessClass(state, c)
